#version 460

#extension GL_EXT_nonuniform_qualifier : enable

#define nonuniform nonuniformEXT

#include "common.glsl"
#include "point_generator.glsl"
#include "..\sampling.glsl"
#include "..\hash.glsl"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(set = 0, binding = 0) uniform sampler3D sdf;

layout(set = 0, binding = 1) buffer Atomics{
    uint nextId;
};

layout(set = 1, binding = 0) buffer Positions{
    Particle particles[];
};

layout(push_constant) uniform Constants{
    layout(offset = 0)
    vec3 boundingBoxLowerCorner;
    float spacing;
    vec3 boundingBoxUpperCorner;
    int genType;
    float jitter;
};

BoundingBox domain = BoundingBox(boundingBoxLowerCorner, boundingBoxUpperCorner);

void main(){
    uvec3 id = gl_GlobalInvocationID;
    uvec3 size = gl_WorkGroupSize * gl_NumWorkGroups;
    uint index = (id.z * size.y + id.y) * size.x + id.x;
    float halfSpacing = spacing * 0.5;

    if(shouldExit(domain.min, domain.max, spacing, genType, id)){
        return;
    }

    float maxJitterDist = 0.5 * jitter * spacing;
    vec3 randomDir = uniformSampleSphere(hash21(index));

    vec3 pos = generatePoint(domain.min, domain.max, spacing, genType, id).xyz + randomDir * maxJitterDist;
    vec3 uv = remap(pos, domain, BoundingBox(vec3(0), vec3(1)));

    float d = texture(sdf, uv).r;

    if(d <= 0){
        uint posIndex = atomicAdd(nextId, 1);
        particles[posIndex].position = vec4(pos, 1);
        particles[posIndex].color = vec4(1, 0, 0, 1);
    }
}