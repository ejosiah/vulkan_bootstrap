#version 460 core
#extension GL_GOOGLE_include_directive : enable
#include "triangle_test.glsl"

#define PI 3.1415926535897932384626433832795
#define TWO_PI 6.283185307179586476925286766559
#define PI_OVER_4 0.78539816339744830961566084581988
#define COLLISION_MARGIN 0.02

layout(local_size_x = 10, local_size_y = 10) in;

struct VertexData{
    vec4 position;
    vec4 color;
    vec3 normal;
    vec3 tangent;
    vec3 bitangent;
    vec2 uv;
};

struct Particle{
    vec3 position;
    vec3 velocity;
};

layout(set = 0, binding = 0) buffer POINT_MASSES_IN{
    Particle particleIn[];
};

layout(set = 1, binding = 0) buffer POINT_MASS_OUT{
    Particle particleOut[];
};

layout(set = 2, binding = 0) uniform MODEL_DATA{
    mat4 xform;
    mat4 xformIT;
    int numTriangles;
};

layout(set = 2, binding = 1) buffer TRI_VERTEX_DATA{
    VertexData tri_vertices[];
};

layout(set = 2, binding = 2) buffer TRI_INDEX_DATA{
    int indices[];
};

layout(set = 3, binding = 0) buffer VERTEX_DATA{
    VertexData vertices[];
};


layout(push_constant) uniform SIM_CONSTANTS {
    vec2 inv_cloth_size;
    float timeStep;
    float mass;
    float ksStruct;
    float ksShear;
    float ksBend;
    float kdStruct;
    float kdShear;
    float kdBend;
    float kd;
    float elapsedTime;
    int simWind;
};

layout(constant_id = 0) const float gravityY = -0.00981f;

vec3 gravity = vec3(0, gravityY, 0);
int width = int(gl_WorkGroupSize.x * gl_NumWorkGroups.x);
int height = int(gl_WorkGroupSize.y * gl_NumWorkGroups.y);
int numPoints = height * width;
int id = int(gl_GlobalInvocationID.y * width + gl_GlobalInvocationID.x);
//int id = int(gl_LocalInvocationIndex);

ivec2 neighbourIndices[12] = {
ivec2(0, 1), ivec2(1, 0), ivec2(0, -1), ivec2(-1, 0),  // structural neigbhours
ivec2(-1, 1), ivec2(1, 1), ivec2(-1, -1), ivec2(1, -1),  // shear neigbhours
ivec2(0, 2), ivec2(0, -2), ivec2(-2, 0), ivec2(2, 0)    // bend neigbhours
};

bool neighbour(int i, out int nid, out ivec2 coord){
    coord = neighbourIndices[i];
    ivec2 index =  coord + ivec2(gl_GlobalInvocationID.xy);
    if(index.x < 0 || index.x >= width || index.y < 0 || index.y >= height){
        return false;
    }
    nid = index.y * width + index.x;
    return true;
}

bool neighbour(int i, out vec3 pos, out vec3 prev_pos, out float ks, out float kd, out float rest_length){
    ivec2 coord;
    int nid;
    if(!neighbour(i, nid, coord)){
        return false;
    }

    pos = particleIn[nid].position.xyz;
    prev_pos = particleOut[nid].position.xyz;

    rest_length = length(coord * inv_cloth_size);
    if(i < 4){
        ks = ksStruct;
        kd = kdStruct;
    }else if(i < 8){
        ks = ksShear;
        kd = kdShear;
    }else if(i < 12){
        ks = ksBend;
        kd = kdBend;
    }

    return true;
}

void unpackTriangle(int index, out vec3 a, out vec3 b, out vec3 c){
    int i0 = indices[3 * index + 0];
    int i1 = indices[3 * index + 1];
    int i2 = indices[3 * index + 2];
    a = (xform * tri_vertices[i0].position).xyz;
    b = (xform * tri_vertices[i1].position).xyz;
    c = (xform * tri_vertices[i2].position).xyz;
}


vec3 unpackNormal(int index){
    int i = indices[3 * index + 0];

    return mat3(xformIT) * tri_vertices[i].normal;
}


bool triangleLineTest(vec3 a, vec3 b, vec3 c, vec3 p, vec3 q, out float t, out vec3 uvw){
    vec3 ab = b - a;
    vec3 ac = c - a;
    vec3 qp = p - q;

    vec3 n = cross(ab, ac);

    float d = dot(qp, n);
    if(d <= 0) return false;

    vec3 ap = p - a;
    t = dot(ap, n);
    if(t < 0) return false;
    if(t > d) return false;

    vec3 e = cross(qp, ap);
    float v = dot(ac, e);
    if(v < 0 || v > d) return false;
    float w = -dot(ab, e);
    if(w < 0 || v + w > d) return false;

    float ood = 1 / d;
    t *= ood;
    uvw.y = v * ood;
    uvw.z = w * ood;
    uvw.x = 1 - v - w;
    return true;

}

void collisionTest(vec3 prev_pos, inout vec3 pos){
   // particleOut[id].color = vec4(1, 0, 0, 1);
    float t = 10.0;
    bool collision = false;
    vec3 uvw;
    vec3 c_point;
    vec3 p = prev_pos;
    vec3 q = pos;
    vec3 n;
    for(int i = 0; i < numTriangles; i++){
        vec3 a, b, c;
        unpackTriangle(i, a, b, c);
        float local_t;
        vec3 local_uvw;
        bool local_collison = triangleLineTest(a, b, c, p, q, local_t, local_uvw);
        if(local_collison && local_t <= t){
            collision = true;
            uvw = local_uvw;
            t = local_t;
            c_point = p + (q - p) * t;
            n = unpackNormal(i);
     //       particleOut[id].color = vec4(0, 1, 0, 1);
        }
    }

    if(collision){

        if(bool(simWind)){
            vec3 v = (pos - prev_pos)/timeStep;
            float sv = dot(v, n);
            if(sv < 0) return;
        }
        pos = p - (q - p) * (t + COLLISION_MARGIN);
        vertices[id].color.rgb = vec3(0, 1, 0);
    }
}


vec3 calculateNormal(vec3 position){
    ivec2 coord;
    int nid;
    int count = 0;
    vec3 normal = vec3(0);
    for(int i = 1; i <= 4; i++){
        if(!neighbour(i - 1, nid, coord)) continue;
        vec3 A = particleIn[nid].position.xyz - position;

        if(!neighbour(i%4, nid, coord)) continue;
        vec3 B = particleIn[nid].position.xyz - position;
        normal += cross(B, A);
        count++;
    }
    normal /= count;
    return normalize(normal);
}

void main(){
    //    float dt = 0.00083;
    float dt = timeStep;
    float inv_dt = 1/dt;
    vec3 pos = particleIn[id].position;
    vec3 N = vertices[id].normal;
    vec3 prev_pos = particleOut[id].position;
    vec3 velocity = (pos - prev_pos) * inv_dt;

    vec3 force = mass * gravity + kd * -velocity;

    if(bool(simWind)){
        float t = elapsedTime;
        float f = 2 * TWO_PI;
        float amp = 0.2;

        vec3 wind = vec3(0);
        wind.x = amp * sin(pos.x * f + t) * sin(pos.y * f + t);
        wind.y = amp * sin(pos.y * f + t) * sin(pos.z * f + t);
        wind.z = amp * sin(pos.z * f + t) * sin(pos.x * f + t);
        force += kd * abs(dot(wind - velocity, N));
    }

    for(int i = 0; i < 12; i++){
        vec3 nPos;
        vec3 nPrev_pos;
        float ks;
        float kd;
        float l0;

        if(!neighbour(i, nPos, nPrev_pos, ks, kd, l0)){
            continue;
        }

        vec3 d = nPos - pos;
        float l1 = length(d);
        d /= l1;
        vec3 nVelocity = (nPos - nPrev_pos) * inv_dt;

        vec3 f = d * (ks * (l1 - l0) + kd * dot(nVelocity - velocity, d));

        force += f;
    }

    float inv_mass = 1.0f/mass;
    if(id == (numPoints - width) || id == (numPoints - 1)){
        inv_mass = 0;
    }

    vec3 a = force * inv_mass;

    vec3 p = 2 * pos - prev_pos + a * dt * dt;

    collisionTest(pos, p);

    p.y = max(p.y, COLLISION_MARGIN);

    particleOut[id].position = p;
    vertices[id].position.xyz = p;
    vertices[id].normal = calculateNormal(p);
}