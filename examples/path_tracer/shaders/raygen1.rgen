#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_debug_printf : require

#define USE_PCG 0
#define MIN_BOUNCES 3

#define RIS_CANDIDATES_LIGHTS 8

// Enable this to cast shadow rays for each candidate during resampling. This is expensive but increases quality
#define SHADOW_RAY_IN_RIS 0

#include "constants.glsl"
#include "ray_tracing_lang.glsl"
#include "random.glsl"
#include "common.glsl"
#include "sampling.glsl"
#include "octahedral.glsl"
#include "util.glsl"
#include "brdf.glsl"

layout(set = 0, binding = 0) uniform accelerationStructure topLevelAs;
layout(set = 0, binding = 1) uniform CameraProperties{
    mat4 viewInverse;
    mat4 projInverse;
} cam;
layout(set = 0, binding = 2, rgba32f) uniform image2D image;
layout(set = 0, binding = 3) uniform sampler2D envMap;

layout(set = 1, binding = 0, std430) buffer MATERIALS{
    Material m[];
} materials[];


layout(binding = 1, set = 1) buffer MATERIAL_ID {
    int i[];
} matIds [];


layout(binding = 2, set = 1) buffer OBJECT_INSTANCE {
    SceneObject sceneObjs[];
};

layout(binding = 0, set = 2) buffer VERTEX_BUFFER {
    Vertex v[];
} vertices[];

layout(binding = 1, set = 2) buffer INDEX_BUFFER {
    int i[];
} indices[];

layout(binding = 2, set = 2) buffer VETEX_OFFSETS {
    VertexOffsets vo[];
} offsets[];

layout(set = 3, binding = 0) buffer LIGHTS {
    Light lights[];
};

layout(set = 4, binding = 0) uniform sampler2D environmentMap;
layout(set = 4, binding = 1) uniform sampler2D pConditionalVFunc;
layout(set = 4, binding = 2) uniform sampler2D pConditionalVCdf;
layout(set = 4, binding = 3) uniform sampler1D pMarginal;
layout(set = 4, binding = 4) uniform sampler1D pMarginalCdf;

layout(push_constant) uniform SceneConstants {
    uint maxBounces;
    uint frame;
    uint currentSample;
    uint numSamples;
    int numLights;
    int adaptiveSampling;
    float worldRadius;
    float pMarginalIntegral;
    uint mask;
    float exposure;
    float skyIntensity;
};

layout(location = 0) rayPayload HitData hitData;
layout(location = 1) rayPayload bool isShadowed;

vec3 loadEnvColor(vec3 direction);

void getTriangle(int objId, mat4x3 xform, uint offsetId, uint primitiveId, out Vertex v0, out Vertex v1, out Vertex v2);

Surface getSurfaceData(HitData hitData, vec3 wo);

// Calculates probability of selecting BRDF (specular or diffuse) using the approximate Fresnel term
float getBrdfProbability(vec3 V, vec3 sN, vec3 albedo, float metalness);
float getBrdfProbability(Surface surface, vec3 wo);

vec3 evalBrdf(Surface surface, vec3 wo, vec3 wi);

vec3 evalIndirectBrdf(Surface surface, int brdfType, vec3 wo, inout RngStateType rngState, out vec3 wi);

bool sampleLightRIS(inout RngStateType rngState, Surface surface, out LightInfo selectedSampl, out float lightSampleWeight);

LightInfo sampleLight(inout RngStateType rngState, Surface surface, out float lightWeight);

bool isVisisble(LightInfo light, Surface surface);

void main(){
    uint N =  gl_LaunchSize.x * gl_LaunchSize.y * maxBounces * numSamples;
    RngStateType rngState = initRNG(vec2(gl_LaunchID), vec2(gl_LaunchSize), frame);
    vec2 offset = vec2(rand(rngState), rand(rngState));
    const vec2 pixelCenter = vec2(gl_LaunchID.xy) + offset;

    const vec2 uv = pixelCenter/vec2(gl_LaunchSize.xy);
    vec2 d = uv * 2.0 - 1.0;

    vec3 origin = vec3(cam.viewInverse * vec4(0,0,0,1));
    vec3 target = vec3(cam.projInverse * vec4(d.x, d.y, 1, 1));
    vec3 direction = vec3(cam.viewInverse*vec4(normalize(target.xyz), 0));

    float tmin = 0;
    float tmax = FLT_MAX;


    if(currentSample > 10 && currentSample >= numSamples){
        return;
    }

    vec3 Lo = vec3(0);
    vec3 throughput = vec3(1);
    int sId = -1;
    for(uint bounce = 0; bounce < maxBounces; bounce++){
        traceRay(topLevelAs, gl_RayFlagsOpaque, mask, 0, 0, 0, origin, tmin, direction, tmax, 0);
        if(!hitData.hit){
            Lo += throughput * loadEnvColor(direction);
            break;
        }

        if(bounce == 0){
            sId = hitData.instanceId;
        }

        vec3 wo = -direction;
        Surface surface  = getSurfaceData(hitData, wo);
        Lo += throughput * surface.emission;

        // direct light contribution
        float lightWeight;
        LightInfo light;
        if(sampleLightRIS(rngState, surface, light, lightWeight)){
            if(lightWeight != 0){
                if(bool(SHADOW_RAY_IN_RIS) || isVisisble(light, surface)){
                    vec3 wi = normalize(light.x - surface.x);
                    Lo += throughput * light.radiance * evalBrdf(surface, wo, wi) * lightWeight;
                }
            }
        }

        if(bounce >= maxBounces - 1){
            break;
        }

        int brdfType;
        if(isMirror(surface)){
            brdfType = BRDF_SPECULAR;
        }else {
            float brdfProbability = getBrdfProbability(surface, wo);
            if(rand(rngState) < brdfProbability){
                brdfType = BRDF_SPECULAR;
                throughput /= brdfProbability;
            }else{
                brdfType = BRDF_DIFFUSE;
                throughput /= (1 - brdfProbability);
            }
        }

        vec3 brdfWeight = evalIndirectBrdf(surface, brdfType, wo, rngState, direction);
        if(isBlack(brdfWeight)){
            break;
        }

        throughput *= brdfWeight;


        origin = offsetRay(surface.x, surface.gN);
    }

    Lo = 1 - exp(-Lo * exposure);
    Lo = linearToSrgb(Lo);
    float t = 1/float(currentSample + 1);
    vec3 dst = imageLoad(image, ivec2(gl_LaunchID.xy)).rgb;
    Lo = mix(dst, Lo, t);
    
    imageStore(image, ivec2(gl_LaunchID.xy), vec4(Lo, 1));
}


vec3 loadEnvColor(vec3 direction){
    vec2 suv = sampleSphere(direction);
    return texture(envMap, suv).rgb * skyIntensity;
}

Surface getSurfaceData(HitData hitData, vec3 wo){
    Surface surface;
    Vertex v0, v1, v2;
    SceneObject sceneObj = sceneObjs[hitData.instanceId];
    int objId = sceneObj.objId;
    getTriangle(objId, hitData.objToWorld, hitData.vertexOffsetId, hitData.primitiveId, v0, v1, v2);
    float u = 1 - hitData.attribs.x - hitData.attribs.y;
    float v = hitData.attribs.x;
    float w = hitData.attribs.y;

    surface.x = u * v0.position + v * v1.position + w * v2.position;
    surface.sN = normalize(u * v0.normal + v * v1.normal + w * v2.normal);
    vec3 e0 = v1.position - v0.position;
    vec3 e1 = v2.position - v0.position;
    surface.gN = normalize(cross(e0, e1));

    VertexOffsets offset = offsets[objId].vo[hitData.vertexOffsetId];
    int matId = matIds[objId].i[hitData.primitiveId + offset.material];
    Material material = materials[objId].m[matId];
    surface.albedo = material.diffuse;
    surface.emission = material.emission;
    surface.metalness = material.metalness.x;
    surface.roughness = material.roughness;

    if(hitData.instanceId == 8){
        surface.albedo = checkerboard(surface.x, surface.gN);
    }

    if(dot(surface.gN, wo) < 0){
        surface.gN *= -1;
        surface.sN *= -1;
    }

    return surface;
}

void getTriangle(int objId, mat4x3 xform, uint offsetId, uint primitiveId, out Vertex v0, out Vertex v1, out Vertex v2){
    uint vertexOffsetId = 0;
    VertexOffsets offset = offsets[objId].vo[offsetId];

    ivec3 index = ivec3(
    indices[objId].i[offset.firstIndex + 3 * primitiveId + 0],
    indices[objId].i[offset.firstIndex + 3 * primitiveId + 1],
    indices[objId].i[offset.firstIndex + 3 * primitiveId + 2]
    );

    v0 = vertices[objId].v[index.x + offset.vertexOffset];
    v1 = vertices[objId].v[index.y + offset.vertexOffset];
    v2 = vertices[objId].v[index.z + offset.vertexOffset];

    v0.position = (xform * vec4(v0.position, 1)).xyz;
    v1.position = (xform * vec4(v1.position, 1)).xyz;
    v2.position = (xform * vec4(v2.position, 1)).xyz;

    mat3 nMatrix = inverse(transpose(mat3(xform)));

    v0.normal = nMatrix * v0.normal;
    v1.normal = nMatrix * v1.normal;
    v2.normal = nMatrix * v2.normal;
}

float getBrdfProbability(vec3 V, vec3 sN, vec3 albedo, float metalness) {

    // Evaluate Fresnel term using the shading normal
    // Note: we use the shading normal instead of the microfacet normal (half-vector) for Fresnel term here. That's suboptimal for rough surfaces at grazing angles, but half-vector is yet unknown at this point
    vec3 specularF0 = vec3(luminance(baseColorToSpecularF0(albedo, metalness)));
    float diffuseReflectance = luminance(baseColorToDiffuseReflectance(albedo, metalness));
    float Fresnel = saturate(luminance(evalFresnel(specularF0, shadowedF90(specularF0), max(0.0f, dot(V, sN)))));

    // Approximate relative contribution of BRDFs using the Fresnel term
    float specular = Fresnel;
    float diffuse = diffuseReflectance * (1.0f - Fresnel); //< If diffuse term is weighted by Fresnel, apply it here as well

    // Return probability of selecting specular BRDF over diffuse BRDF
    float p = (specular / max(0.0001f, (specular + diffuse)));

    // Clamp probability to avoid undersampling of less prominent BRDF
    return clamp(p, 0.1f, 0.9f);
}

float getBrdfProbability(Surface surface, vec3 wo){
    return getBrdfProbability(wo, surface.sN, surface.albedo, surface.metalness);
}

vec3 evalBrdf(Surface surface, vec3 wo, vec3 wi){
    BrdfArgs args;
    args.wo = wo;
    args.wi = wi;
    args.surfacePoint = surface.x;
    args.surfaceNormal = surface.sN;
    args.surfaceGeomNormal = surface.gN;
    args.surfaceAlbedo = surface.albedo;
    args.surfaceMetalness = surface.metalness;
    args.surfaceRoughness = surface.roughness;

    return evalCombinedBRDF(args);
}

vec3 evalIndirectBrdf(Surface surface, int brdfType, vec3 wo, inout RngStateType rngState, out vec3 wi){

    BrdfArgs args;
    args.wo = wo;
    args.surfacePoint = surface.x;
    args.surfaceNormal = surface.sN;
    args.surfaceGeomNormal = surface.gN;
    args.surfaceAlbedo = surface.albedo;
    args.surfaceMetalness = surface.metalness;
    args.surfaceRoughness = surface.roughness;
    args.rngState = rngState;
    args.brdfType = brdfType;

    vec3 sampleWeight = evalIndirectCombinedBRDF(args);

    rngState = args.rngState;
    wi = args.wi;
    return sampleWeight;
}

float fallOff(Light light, vec3 pointToLightDir){
    vec3 n = light.normal;
    float cutOff = light.cosWidth;
    float fallOffStart = light.fallOffStart;

    float cos0 = max(0, dot(pointToLightDir, n));
    if(cos0 < cutOff) return 0;
    if(cos0 > fallOffStart) return 1;
    return smoothstep(cutOff, fallOffStart, cos0);
}

bool sampleLightRIS(inout RngStateType rngState, Surface surface, out LightInfo selectedSample, out float lightSampleWeight){
    if(numLights <= 0) return false;

    float totalWeights = 0;
    float samplePdfG = 0;

    for(int i = 0; i < RIS_CANDIDATES_LIGHTS; i++){
        float lightWeight;
        LightInfo light = sampleLight(rngState, surface, lightWeight);
        if(isBlack(light.radiance)){
            continue;
        }

            #if SHADOW_RAY_IN_RIS
        if(!isVisisble(light, surface)){
            continue;
        }
            #endif

        float candidatePdfG = luminance(light.radiance);
        float candidateRISWeight = candidatePdfG * lightWeight;

        totalWeights += candidateRISWeight;
        if(rand(rngState) < (candidateRISWeight / totalWeights)){
            selectedSample = light;
            samplePdfG = candidatePdfG;
        }
    }

    if(totalWeights == 0.0f){
        return false;
    }else {
        lightSampleWeight = (totalWeights / float(RIS_CANDIDATES_LIGHTS)) / samplePdfG;
        return true;
    }

}

LightInfo sampleLight(inout RngStateType rngState, Surface surface, out float lightWeight){
    LightInfo lightInfo;
    lightInfo.radiance = vec3(0);
    lightInfo.sx = surface.x;
    lightInfo.sn = surface.sN;
    lightInfo.pdf = 0;
    lightWeight = 0;
    if(numLights <= 0){
        return lightInfo;
    }
    int id = int(rand(rngState) * numLights);
    Light light = lights[id];
    lightInfo.x = light.position;
    lightInfo.n = light.normal;
    lightInfo.area = light.area;
    lightInfo.flags = light.flags;
    lightInfo.value = light.value;
    lightInfo.pdf = 0.0;

    if(isPositional(light)){
        vec3 wi = light.position - lightInfo.sx;
        lightInfo.wi = normalize(wi);
        lightInfo.dist = length(wi);
        lightInfo.NdotL = clamp(dot(lightInfo.wi, lightInfo.sn), 0.00001, 1);
        lightInfo.pdf = 1;
        lightInfo.radiance = light.value * fallOff(light, -lightInfo.wi) / (lightInfo.dist * lightInfo.dist);
    }

    if(isDistant(light)){
        lightInfo.wi = -light.normal;
        lightInfo.x = lightInfo.sx + lightInfo.wi  * 2 * worldRadius;
        lightInfo.dist = distance(lightInfo.x, lightInfo.sx);
        lightInfo.NdotL = clamp(dot(lightInfo.wi, lightInfo.sn), 0.00001, 1);
        lightInfo.pdf = 1;
        lightInfo.radiance = light.value;
    }

    if(isArea(light)){
        SceneObject sceneObj = sceneObjs[light.instanceId];
        int objId = sceneObj.objId;

        uint primitiveId = uint(floor(rand(rngState) * light.numTriangles));

        Vertex v0, v1, v2;
        getTriangle(objId, mat4x3(1), light.triangleOffset, primitiveId, v0, v1, v2);

        vec2 u = vec2(rand(rngState), rand(rngState));
        vec2 uv = uniformSampleTriangle(u);
        lightInfo.x = u.x * v0.position + u.y * v1.position + (1 - u.x - u.y) * v2.position;
        lightInfo.n = u.x * v0.normal + u.y * v1.normal + (1 - u.x - u.y) * v2.normal;

        vec3 wi = lightInfo.x - lightInfo.sx;
        lightInfo.wi = normalize(wi);
        lightInfo.dist = length(wi);
        lightInfo.NdotL = clamp(dot(lightInfo.wi, lightInfo.sn), 0.00001, 1);
        lightInfo.radiance = light.value;
        lightInfo.pdf = (lightInfo.dist * lightInfo.dist)/(lightInfo.NdotL * light.area);
    }

    if(isInfinite(light)){
        vec2 u = vec2(rand(rngState), rand(rngState));
        float pdf;
        vec2 uv = sampleContinuous2D(pConditionalVFunc, pConditionalVCdf, pMarginal, pMarginalCdf, pMarginalIntegral, u, pdf);
        if(pdf == 0){
            lightInfo.value = vec3(0);
            lightInfo.pdf = 0;
            return lightInfo;
        }
        float sinTheta;
        light.normal = -direction_from_spherical(uv, sinTheta);
        lightInfo.wi = direction_from_spherical(uv, sinTheta);
        lightInfo.x = lightInfo.sx + lightInfo.wi  * 2 * worldRadius;
        lightInfo.dist = distance(lightInfo.x, lightInfo.sx);
        lightInfo.NdotL = clamp(dot(lightInfo.wi, lightInfo.sn), 0.00001, 1);
        lightInfo.pdf = pdf / ( 2 * PI * PI * sinTheta);
        if(sinTheta == 0){
            lightInfo.pdf = 0;
        }
        lightInfo.value = texture(environmentMap, uv).rgb;
        lightInfo.radiance = texture(environmentMap, uv).rgb ;
    }

    lightInfo.radiance = dot(lightInfo.wi, lightInfo.sn) < 0.00001 ? vec3(0) :  lightInfo.radiance;
    lightWeight = 1/lightInfo.pdf;
    return lightInfo;
}

bool isVisisble(LightInfo light, Surface surface){
    isShadowed = true;
    vec3 origin = offsetRay(surface.x, surface.gN);
    vec3 direction = normalize(light.x - surface.x);
    float dist = distance(light.x, surface.x);
    uint flags = gl_RayFlagsTerminateOnFirstHit | gl_RayFlagsSkipClosestHitShader;
    traceRay(topLevelAs, flags, (mask & ~OBJECT_TYPE_LIGHT), 0, 0, 1, origin, 0, direction, dist, 1);

    return !isShadowed;
}